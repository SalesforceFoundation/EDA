/*
    Copyright (c) 2020, Salesforce.org
    All rights reserved.

    Redistribution and use in source and binary forms, with or without
    modification, are permitted provided that the following conditions are met:

    * Redistributions of source code must retain the above copyright
      notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright
      notice, this list of conditions and the following disclaimer in the
      documentation and/or other materials provided with the distribution.
    * Neither the name of Salesforce.org nor the names of
      its contributors may be used to endorse or promote products derived
      from this software without specific prior written permission.

    THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
    "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
    LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
    FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
    COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
    INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
    BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
    LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
    CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
    LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
    ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
    POSSIBILITY OF SUCH DAMAGE.
*/
/**
* @author Salesforce.org
* @date 2020
* @group Cases
* @group-content ../../ApexDocContent/Case.htm
* @description This class prevents Case records from being deleted if
* it has any Behavior Involvement associations.
*/
public with sharing class CASE_CannotDelete_TDTM extends TDTM_Runnable {
    /*******************************************************************************************************
    * @description Get the setting of preventing Case deletion
    */
    private static Boolean enabledPreventCaseDeletion = UTIL_CustomSettingsFacade.getSettings().Prevent_Case_Deletion__c;

    /*******************************************************************************************************
    * @description Prevents Case records from being deleted if it has any Behavior Involvement associations.
    * @param newList the list of Case from trigger new.
    * @param oldList the list of Case from trigger old.
    * @param triggerAction which trigger event (BeforeDelete).
    * @param objResult the describe for Case.
    * @return dmlWrapper.
    ********************************************************************************************************/
     public override DmlWrapper run(List<SObject> newList, List<SObject> oldList,
     TDTM_Runnable.Action triggerAction, Schema.DescribeSObjectResult objResult) {
        if (!enabledPreventCaseDeletion) {
            return new DmlWrapper();
        }

        Map<ID, Case> oldmap = new Map<ID, Case>((List<Case>)oldlist);

        if (triggerAction == TDTM_Runnable.Action.BeforeDelete) {
            for (Case caseRecord : [SELECT Id,
                                        (SELECT Id FROM Behavior_Involvements__r LIMIT 1)
                                    FROM Case
                                    WHERE Id IN :oldlist]) {
                if (caseRecord.Behavior_Involvements__r.size() > 0) {
                    Case caseInContext = oldmap.get(caseRecord.Id);
                    caseInContext.addError(Label.CannotDelete);
                }
            }
        }
        return new DmlWrapper();
    }
}